plugins {
	id 'org.springframework.boot' version '2.4.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'com.palantir.docker' version '0.26.0'
	id 'java'
}

group = 'com.food'
version = "1.0.0-${ext['build.number']}"
description = "Gateway from food platform"
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2020.0.2")
}

dependencies {
	implementation('org.springframework.cloud:spring-cloud-starter-gateway')
	implementation('org.springframework.boot:spring-boot-starter-actuator')
	implementation 'org.springdoc:springdoc-openapi-webflux-core:1.6.14'
	implementation 'org.springdoc:springdoc-openapi-webflux-ui:1.6.14'
	implementation("org.springframework.cloud:spring-cloud-starter-kubernetes-fabric8-config")
	implementation('org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j')
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('org.springframework.cloud:spring-cloud-starter-contract-stub-runner') {
		exclude (group: 'org.springframework.boot', module: 'spring-boot-starter-web')
	}
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
}

processResources {
	filesMatching("**/application.yml") {
		expand(project.properties)
	}
}

bootJar {
	archiveFileName = "app.jar"
}

docker {
	name "${project.name}:${version}"
	tag 'DockerHub', "cezbatistao/${project.name}:${version}"
	buildArgs(['name': "${project.name}"])
	copySpec.from("build").into("build")
	pull true
	dockerfile file("Dockerfile")
	noCache true
}
